//*****************************************************************************
// Title:         AnalyticObjectTest
//*****************************************************************************

package com.industry.analytics.Analyticobj_cor_024;

using com.industry.analytics.Analytic;
using com.industry.analytics.AnalyticObject;
using com.industry.analytics.AnalyticInterface;
using com.industry.analytics.Data;

event NextStep { 
	integer id;
}

monitor AnalyticObjectTest {
	integer id := 0;
	
	action onload()	{
		monitor.subscribe("mainCtx");

		// Create the Analytic event
		Analytic t := Analytic("Test", ["Input1"], ["Output1"],
								 {"boolean_param":"true",
								  "decimal_param":"1.0",
								  "float_param":"2.0",
								  "integer_param":"3",
								  "string_param":"a_string"});
		
		// Create a new Analytic object
		(new AnalyticObject).initExtended( t,
		                            processData1,
		                            new action<dictionary<string, Data> >,
		                            false,
		                            new action<>,
		                            new action<>,
		                            initComplete );

		// Create another new Analytic object using the same Analytic
		(new AnalyticObject).initExtended( t,
		                            processData2,
		                            new action<dictionary<string, Data> >,
		                            false,
		                            new action<>,
		                            new action<>,
		                            initComplete );
	}
	
	action initComplete( boolean success, AnalyticInterface ti )	{

		// Increment a global counter so our NextStep listeners wont 
		// interfere with each other
		id := id+1;
		integer localId := id;
		
		if( success ) then {
			Data currData1 := Data( "Input1", com.industry.analytics.DataConstants.RAW,
			                             "TEST_ASSET_1", currentTime.toDecimal(), 111.1d, "TEST_STRING_1",
			                             1.0, 2.0, 3.0,
			                             new dictionary<string, string> );
			Data currData2 := Data( "Input1", com.industry.analytics.DataConstants.RAW,
			                             "TEST_ASSET_2", currentTime.toDecimal(), 222.2d, "TEST_STRING_2",
			                             1.0, 2.0, 3.0,
			                             new dictionary<string, string> );
			Data currData3 := Data( "Input2", com.industry.analytics.DataConstants.RAW,
			                             "TEST_ASSET_3", currentTime.toDecimal(), 333.3d, "TEST_STRING_3",
			                             1.0, 2.0, 3.0,
			                             new dictionary<string, string> );
			Data currData4 := Data( "Input2", com.industry.analytics.DataConstants.RAW,
			                             "TEST_ASSET_4", currentTime.toDecimal(), 444.4d, "TEST_STRING_4",
			                             1.0, 2.0, 3.0,
			                             new dictionary<string, string> );
			
			// Send the data events into the new Analytic.
			// This should not do anything as we've not started the Analytic
			send currData1 to "Input1";
			send currData2 to "Input1";
			send currData3 to "Input1";
			send currData4 to "Input1";
			
			// Start the Analytic 
			// We send the internal event to ensure we are starting 
			// the Analytic after the data events above are sent
			send NextStep(localId) to "mainCtx";
			on NextStep(id=localId) {
				// Start the Analytic
				ti.start();

				// Send the data events into the new Analytic again
				// These events should be handled
				send currData1 to "Input1";
				send currData2 to "Input1";
				send currData3 to "Input1";
				send currData4 to "Input1";

				// Stop the Analytic
				// We send the internal event to ensure we are starting 
				// the next step after the data events above are sent
				send NextStep(localId) to "mainCtx";
				on NextStep(id=localId) {
					// Stop the Analytic
					ti.stop();
					
					// Send the data events into the new Analytic again
					// These events should be ignored
					send currData1 to "Input1";
					send currData2 to "Input1";
					send currData3 to "Input1";
					send currData4 to "Input1";
				}
			}			
		} else {
			log "FAILED TO CREATE ANALYTICER INTERFACE" at ERROR; 
		}
    }
	
	// Handler for Analytic1
	action processData1( Data currData ) {
		log "PROCESS DATA 1 CALLED: "+currData.toString();
	}

	// Handler for Analytic2
	action processData2( Data currData ) {
		log "PROCESS DATA 2 CALLED: "+currData.toString();
	}
}
