//*****************************************************************************
// Title: Volatility Analytic implementation
//
// Copyright (c) 2015-2017 Software AG, Darmstadt, Germany and/or its licensors.
//
// Licensed under the Apache License, Version 2.0 (the "License");
// you may not use this file except in compliance with the License.
// You may obtain a copy of the License at
//
//     http://www.apache.org/licenses/LICENSE-2.0
//
// Unless required by applicable law or agreed to in writing, software
// distributed under the License is distributed on an "AS IS" BASIS,
// WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
// See the License for the specific language governing permissions and
// limitations under the License.
//*****************************************************************************

package com.industry.analytics.streaming_calculations;

using com.industry.analytics.Data;
using com.industry.analytics.DataConstants;
using com.industry.analytics.Analytic;
using com.industry.analytics.AnalyticObject;
using com.industry.analytics.AnalyticInterface;
using com.industry.analytics.Constants;
using com.industry.analytics.TimeWeightedVariance;

/* @AnalyticDefinition
{
	"name": "Volatility",
	"description": "Generates volatility data from the input data.",
	"group": "Streaming Calculations",
	"documentation": "com/industry/analytics/streaming_calculations/Volatility.html",
	"inputChannels": [
		{
			"name": "Data",
			"description": "The channel receving the data for the Volatility calculation",
            "dataProperties": ["dValue"] 
		}
	],
	"outputChannels": [
		{
			"name": "Volatility",
			"description": "The channel will be the name of the stream that contains the Volatility calculations",
            "dataProperties": ["+dValue"]
        },
        {
			"name": "Moving Average",
			"description": "The channel will be used for optional Moving Average calculations, if enabled",
            "optional": true,
            "dataProperties": ["+dValue", "-params", "-sValue", "-xValue", "-yValue", "-zValue"]
        }
	],
	"properties": [
		{
			"name": "timeWindow",
			"description": "Defines the time window to calculate the Volatility over",
			"type": "decimal",
			"defaultValue": 60,
			"optional": true,
            "validator": "function(value) { return value >=0 || 'Value must be greater than or equal to 0.0' }",
			"postfix": "(Seconds)"
		},{
			"name": "type",
			"description": "Defines whether to use variance or standard deviation (I.E. the square root of the Volatility).",
			"type": "string",
			"defaultValue": "variance",
			"validValues": ["stddev", "variance"],
			"optional": true
		},{
			"name": "bySourceId",
			"description": "Defines whether or not the standard deviation is calculated by source id of Data event.",
			"type": "boolean",
            "defaultValue": true,
            "optional": true,
			"advanced":true
		},{
            "name": "managementId",
            "description": "Defines the id used for management of the analytic",
            "type": "string",
            "optional": true,
            "advanced": true
        }
	]
}
*/

/**
 *  The Volatility Analytic computation generates a Data containing 
 *  a Volatility calculation for each input Data received. 
 * 
 *  During the course of the calculation, the moving average is 
 *  calculated as per the MovingAverage Analytic.
 *  If this value is required it can also be output from this computation
 *  as a more performant option than the doing the calculation a
 *  second time within a MovingAverage Analytic.  The Data events 
 *  for the moving Average calculation are sent on the Analytics 2nd
 *  <font face="courier" size="-1">outputDataNames</font> parameter value.  
 *
 *  The following formulas are implemented: 
 * 
 *  Standard Moving Average calculation:<font face="courier" size="-1"><br><br>
 *  ma_n = a * x_n + (1 - a) * ma_n-1<br>
 *  &nbsp;&nbsp;&nbsp;&nbsp;&nbsp;= ma_n-1 + a * (x_n - ma_n-1)<br>
 *  a = (1 - e^-u)<br>
 *  u = (t_n - t_n-1) / T<br>
 *  <br><br>
 *  Re-arranged Moving Average calculation for efficiency:<br><br>
 *  ma_n = (1 - a) * x_n + a * ma_n-1<br>
 *  &nbsp;&nbsp;&nbsp;&nbsp;&nbsp;= x_n + a * (ma_n-1 - x_n)<br>
 *  a = e^-u<br>
 *  u = (t_n - t_n-1) / T<br>
 *
 *  Volatility:<br>
 *  S_n = a * S_n-1 + (1 - a)(x_n - ma_n)(x_n - ma_n-1)<br>
 *  &nbsp;&nbsp;&nbsp;&nbsp;= a * (S_n-1 + (1 - a)(x_n - ma_n-1)^2)<br>
 *  </font>
 *
 *  Note: If there are multiple values (x_n) for a particular timestamp (t_n) then the average is used
 *  Note: If t_n < t_n-1 then the value is ignored
 *
 *  <dl><dt><b>Input Data events:</b></dt>
 *  <dd>Only a single input Data stream name must be provided.</dd>
 *  <dd><b>Note:</b> The input Data events timestamp value should be defined as the number of seconds since the epoch (1st January 1970).</dd>
 *  </dl>
 *  <dl><dt><b>Output Data events:</b></dt>
 *  <dd>A maximum of two output Data stream names may be provided.<br>
 *      The first value must always be defined and will be the name of the
 *      stream that contains the Volatility calculations. If a second output
 *      name is provided, this will be used for the optional Moving Average
 *      calculation. Params are copied to the output data events.</dd>
 *  </dl>
 *  <dl><dt><b>Params:</b></dt>
 *  <dd>
 *  <table border="1" style="border-collapse: collapse;width:100%">
 *  <tr><th>Param Name</th><th>Description</th><th>Valid Values</th><th>Data Type</th><th>Required</th><th>Default Value</th></tr>
 *  <tr><td><b>timeWindow</b></td><td>Defines the time window to calculate the Volatility over</td>
 *      <td>Stringified decimal <font face="courier" size="-1">>=0.0d</font></td><td>Decimal</td><td>False</td><td><font face="courier" size="-1">60.0d</font></td></tr>
 *  <tr><td><b>bySourceId</b></td><td>If defined, the Volatility values are calculated based on the <font face="courier" size="-1">sourceId</font> of the <font face="courier" size="-1">Data</font> events</td>
 *      <td><font face="courier" size="-1">"true"</font> or <font face="courier" size="-1">"false"</font></td><td>Boolean</td><td>False</td><td><font face="courier" size="-1">true</font></td></tr>
 *  <tr><td><b>type</b></td><td>Defines whether to use variance or standard deviation  
 *                                           (I.E. the square root of the Volatility).</td>
 *      <td><font face="courier" size="-1">"stddev"</font> or <font face="courier" size="-1">"variance"</font></td><td>String</td><td>False</td><td><font face="courier" size="-1">variance</font></td></tr>
 *  </table></dd>
 *  </dl>
 *  <dl><dt><b>Example usage:</b></dt>
 <dd><code>
// Define a Volatility Analytic which takes Data events on the channel "Input1"  
// and calculates the volatility/variance value over a 15second timewindow. It publishes
// a Data event with the calculated gradient value on the output channel "Output1".   
send com.industry.analytics.Analytic("Volatility", ["Input1"], ["Output1"], {"timeWindow":"15.0"} ) to "";

// Define a Volatility Analytic which takes Data events on the channel "Input2"  
// and calculates the standard deviation value over a 30second timewindow instead. 
// It publishes a Data event with the calculated value on the output channel "Output2".   
send com.industry.analytics.Analytic("Volatility", ["Input2"], ["Output2"], {"type":"stddev", "timeWindow":"30.0"} ) to "";
</code></dd>
 *</dl>
 *  @see com.industry.analytics.streaming_calculations.Average  The moving Average Analytic.
 */
event Volatility
{
	/** This constant defines the name of the Analytic. */
	constant string NAME := "Volatility";
	
	/** This constant defines the configuration parameter name that
	 *  specifies the time window to calculate the Volatility over */
	constant string TIMEWINDOW := "timeWindow";

	/** This constant defines the configuration parameter name
	 *  that defines whether or not the Volatility values will be 
	 *  partitioned by the <font face="courier" size="-1">sourceId</font>*/
	constant string BY_SOURCEID := "bySourceId";
	
	/** This constant defines the configuration parameter name that
	 *  specifies type of Volatility 
	 *  (I.E. standard deviation or varience) is generated. */
	constant string TYPE := "type";

	// State
	/** Local cached dictionary of the calculated Volatility values, keyed by
	 *  the sourceId.
	 *  @private */	
	dictionary<string /*sourceId*/, TimeWeightedVariance> _volatilities;

	// Algo specific variables
	/** Local cached output Data name
	 *  @private */
	string _outputDataName;
	/** Local cached value for the Data stream name if the movingAverage
	 *  configuration parameter was enabled
	 *  @private */
	string _maDataName;
	
	/** Local cached value for the timeWindow configuration parameter value
	 *  @private */
	decimal _timeWindow;
	/** Local cached value for the type configuration parameter value
	 *  @private */
	string _type;
	/** Local cached value for the movingAverage configuration parameter value
	 *  @private */
	boolean _movingAverage;
	
	/** Local cached value for if we are partioning by the sourceId
	 *  @private */
	boolean _bySourceId;
	
	/**This constant defines the value of configuration parameter type.
	* @private
	*/
	constant string STD_DEV := "stdDev";
	
	/**This constant defines the value of configuration parameter type.
	* @private
	*/
	constant string VARIANCE := "variance";
	
	/** This constant defines the internal name used to define "all sourceIds"
	 *  used when not partitioning by sourceId.
	 *  @private */
	constant string CONST_INTERNAL_ALL_SOURCES := "__internalSourceIdWildcard";
	
	/** The Analytic Base Object implementation 
	 *  @private */
	AnalyticObject _analyticObject;
	

	/** 
	 *  This action creates a new instance of the Analytic.
	 *  Typically, this is called internally by the associated Analytics 
	 *  service monitor when a com.industry.analytics.Analytic event is 
	 *  sent for this type of Analytic.
	 *
	 *  @param  config        The Analytic configuration that 
	 *                        will be used for this instance
	 *  @param  initComplete  The action callback to call when the new Analytic  
	 *                        has been created, or if an error occured.
     *
	 *  @see com.industry.analytics.Analytic           The Analytic configuration event object.
	 *  @see com.industry.analytics.AnalyticInterface  The action interface that can be used 
	 *                                            to interact with the Analytic.
	 */
	action init( Analytic config,
	             action<boolean, AnalyticInterface> initComplete ) {

		if( not _validateConfiguration( config ) ) then {
			initComplete( false, new AnalyticInterface );
			return;
		}
		
		_analyticObject := new AnalyticObject;
		_analyticObject.init( config,
		                      processData,
		                      reset,
		                      initComplete );
	}
	
	/** 
	 *  This helper action validates configuration that was
	 *  used when trying to create a new instance of the Analytic.
	 *  This is called internally, and should not be called directly by 
	 *  the Users application.
	 *  
	 *  @param   config  The Analytic configuration object that is being validated
	 *  @returns A boolean value indicating whether the configuration was valid or not.
	 * 
	 *  @private
	 */
	action _validateConfiguration( Analytic config ) returns boolean {
		boolean result := config.validateParams( NAME, 1, -1, 
		                                         [ TYPE, TIMEWINDOW, BY_SOURCEID ],
		      		                           	 [ Constants.STRING,  Constants.DECIMAL, Constants.BOOLEAN ],
		      		                           	 [ false, false, false] );

		// Only check the specific params if the validation was okay
		if( result ) then {
			if( config.outputDataNames.size() < 1 ) then {
				log "outputDataNames sequence cannot be empty." at ERROR;
				log "  outputDataNames: " + config.outputDataNames.toString() at ERROR;
				result := false;
			} else if config.outputDataNames.size() > 2 then {
				log "outputDataNames sequence should only have a maximum of two entries." at ERROR;
				log "  outputDataNames: " + config.outputDataNames.toString() at ERROR;
				result := false;
			} else {			
				string currOutputName;
				for currOutputName in config.outputDataNames {
					string trimmedName := currOutputName.ltrim().rtrim();
					if( trimmedName.length() = 0 ) then {
						log "outputDataNames cannot contain blank strings." at ERROR;
						result := false;
					}
				}
			}
		}
		
		if( result ) then { 
			_outputDataName := config.outputDataNames[0];
			if( config.outputDataNames.size() = 2 ) then {
				_maDataName := config.outputDataNames[1];
				_movingAverage := true;
			}
			
			// Get the mandatory timeWindow parameter
			_timeWindow := config.getOrDecimal( TIMEWINDOW, 60.0d );
			if( _timeWindow <= 0.0d ) then {
				log "  Param timeWindow must be positive. Given value: " + _timeWindow.toString() at ERROR;
				result := false;
			}
			
			_bySourceId := config.getOrBoolean(BY_SOURCEID, true);

			_type := config.getOrString( TYPE, VARIANCE );
			if( not (_type.toLower() = STD_DEV.toLower() or _type.toLower() = VARIANCE ) ) then	{
				log "  Param "+TYPE+" must be either "+STD_DEV+" or "+VARIANCE+". Given value: " + _type at ERROR;
				result := false;
			}
		} 
		return result;
	}
	
	
	/**
	 *  This action implements the Analytic function itself. 
	 *  This is called internally by the Analytic Object, and  
	 *  should not be called directly by the Users application.
	 *  If the application has created this Analytic using the 
	 *  #init() action directly, then the <font face="courier" size="-1">processData</font>
	 *  action on the <font face="courier" size="-1">com.industry.analytics.AnalyticInterface</font> 
	 *  that was returned should be used instead.
	 * 
	 *  The following formulas are implemented: 
	 * 
	 *  Moving average:
	 *  Standard formulation:
	 *  ma_n = a * x_n + (1 - a) * ma_n-1
	 *       = ma_n-1 + a * (x_n - ma_n-1)
	 *  a = (1 - e^-u)
	 *  u = (t_n - t_n-1) / T
	 *
	 *  Re-arrange for efficiency:
	 *  ma_n = (1 - a) * x_n + a * ma_n-1
	 *       = x_n + a * (ma_n-1 - x_n)
	 *  a = e^-u
	 *  u = (t_n - t_n-1) / T
	 *
	 *  Volatility:
	 *  S_n  = a * S_n-1 + (1 - a)(x_n - ma_n)(x_n - ma_n-1)
	 *       = a * (S_n-1 + (1 - a)(x_n - ma_n-1)^2)
	 *
	 *  @param   dataIn  The input Data event to be processed by this Analytic
	 * 
	 *  @private
	 */
	action processData(Data dataIn)
	{
		log "Processing " + dataIn.toString() at DEBUG;
		

		// If we are using the optional parameter to partition based
		// on the sourceId parameter
		string sourceId := dataIn.sourceId;
		if( not _bySourceId ) then {
			sourceId := CONST_INTERNAL_ALL_SOURCES;
		} 

		if _volatilities.hasKey(sourceId) then
		{
			_volatilities[sourceId].update(dataIn.dValue, dataIn.timestamp);
		}
		else
		{
			TimeWeightedVariance var := new TimeWeightedVariance;
			var.init(dataIn.dValue, dataIn.timestamp, _timeWindow);
			_volatilities[sourceId] := var;
		}
		
		Data dataOut := dataIn.clone();
		dataOut.streamName := _outputDataName;
		dataOut.type       := DataConstants.COMPUTED;
		
		if _type.toLower() = STD_DEV.toLower() then
		{
			dataOut.dValue := _volatilities[sourceId].eStDev();
		}
		else if _type.toLower() = VARIANCE.toLower() then
		{ 
			dataOut.dValue := _volatilities[sourceId].eVar();
		}
		// Send the Data event out 
		_analyticObject.sendData(dataOut);
		
		// If the moving Average param was defined and enabled 
		if _movingAverage then
		{
			dataOut.streamName := _maDataName;
			dataOut.dValue     := _volatilities[sourceId].eMa();
			_analyticObject.sendData(dataOut);
		}
	}


	/**
	*  Optional action to reset the state of the analytic instance
	*  back to its initialisation state.
	*  This is called internally by the Analytic Object, and  
	*  should not be called directly by the Users application.
	*  If the application has created this Analytic using the 
	*  #init() action directly, then the <font face="courier" size="-1">reset</font>
	*  action on the <font face="courier" size="-1">com.industry.analytics.AnalyticInterface</font> 
	*  that was returned should be used instead.
	*
	*  @private
	*/
	action reset() {
		_volatilities.clear();
	}
}


/** 
 *  This internal Monitor is used to automatically intern 
 *  any string constants that are defined that are associated
 *  with the Volatility Analytic in order to improve  
 *  performance when handling strings.
 *
 *  Note: The listener that is created is required in order
 *        to keep the monitor alive between a Persistant  
 *        Correlators recovery.  Otherwise, this would be a 
 *        transient monitor, and it would not be recovered.  
 *        Which would then mean that the strings would not
 *        interned in the recovered Correlator.
 *
 *  @private
 */
monitor InternVariance {
	event StayAlive {}
	
	action onload()	{
		string discard := Volatility.NAME.intern();
		discard := Volatility.TIMEWINDOW.intern();
		discard := Volatility.TYPE.intern();
		discard := Volatility.STD_DEV.intern();
		discard := Volatility.VARIANCE.intern();
		discard := Volatility.CONST_INTERNAL_ALL_SOURCES.intern();
		on all StayAlive() {} 
	}
}
